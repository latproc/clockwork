# This examples defines a shared machine that can only be
# allocated to a single machine at a time. 

Shared MACHINE {
	free INITIAL;
	in_use STATE;
}
shared Shared(tab:Tests);

# the master machine takes two user machines that use the shared object
# and attempts to get them both to engages simultaneously

user1 User (tab:Tests) shared;
user2 User (tab:Tests) shared;

master Master (tab:Tests) user1, user2;

Master MACHINE a, b {

	idle DEFAULT;

	starting DURING go {
		SEND start TO a;
		SEND start TO b;
	}
	TRANSITION idle TO idle USING go;
}

User MACHINE shared {

	action FLAG;
	locked WHEN action != on && shared != free;
	idle WHEN action IS off;
	busy WHEN action IS on;

	starting DURING start {
		LOG "starting";
		LOCK shared;
		SET shared TO in_use;
		SET action TO on; 
		WAIT 200;
	}

	finishing DURING stop {
		LOG "finishing";
		SET shared TO free;
		SET action TO off;
		UNLOCK shared;
	}
		
	ENTER busy { LOG "busy"; }
	ENTER idle { LOG "idle"; }
	ENTER locked { LOG "locked" }

	TRANSITION idle TO busy USING start;
	TRANSITION busy TO idle USING stop;
}


