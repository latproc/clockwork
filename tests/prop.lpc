# Various property tests

g1 CONSTANT(tab:vars) 7;
g2 CONSTANT(tab:vars) 9;
s1 CONSTANT(tab:vars) "Hello";
v1 VARIABLE(tab:vars) 13;
v2 VARIABLE(tab:vars) 0;
v3 VARIABLE(tab:vars) 13;

# PropertyTests is intended to stall indefinitely in the run command
# if a test fails. If this happens the test driver will enter an error
# state.
PropertyTests MACHINE (tab:s1, x:3, y) p1(A:7) {
    OPTION test_step 0;
    OPTION test_label "";
    GLOBAL g1, v1;
    OPTION z g1;
    OPTION tab tab; # dereferences machine property 'tab'
    OPTION x "";

    OPTION pname "p1";
    OPTION p1 "up";
    OPTION p2 "down";
    OPTION pvalue "";
    OPTION state_name "";

    test_flag FLAG;

    idle DEFAULT;
    ok WHEN SELF IS ok OR SELF IS working;

    working DURING run {

        test_label := "Assignment to a VARIABLE";
        INC test_step;
        v1 := 11;
        WAITFOR v1.VALUE == 11;

        test_label := "Assignment to the VALUE property of a VARIABLE";
        INC test_step;
        v2.VALUE := 12;
        WAITFOR v2 == 12;

        test_label := "Setting state from a property value";
        state_name := "on";
        WAITFOR test_flag IS off;
        SET test_flag TO PROPERTY state_name;
        WAITFOR test_flag IS on;

        test_label := "Setting a property indirectly from a property that names a VARIABLE";
        INC test_step;
        pname := "v1";
        pvalue := PROPERTY pname; # set a value by looking up a value indirectly
        WAITFOR pvalue == v1.VALUE;

        test_label := "Setting a property indirectly using an explicit source";
        INC test_step;
        pname := "v2";
    	pvalue := PROPERTY pname OF SELF; # set a value by looking up a value indirectly
        WAITFOR pvalue == v2.VALUE;

        test_label := "Setting a property indirectly from a property on another machine";
        INC test_step;
        x := "VALUE";
        v2 := PROPERTY x OF v3; # set a value by looking up a value indirectly
        WAITFOR v2.VALUE == v3;

        test_label := "Using a property to name the target of an assignment";
        INC test_step;
        PROPERTY x OF v3 := 17;
        WAITFOR v3.VALUE == 17;

        test_label := "Using a property value to target and add a new property";
        INC test_step;
        x := "xxx"; # add a new property
        PROPERTY x OF SELF := "hello " + x;
        WAITFOR xxx == "hello xxx";
    }
}

f FLAG;
property_tests PropertyTests(pname:"p1") f;

Test_PropDriver MACHINE script {
    OPTION execution_timeout 10000;
    error WHEN  SELF IS error || SELF IS waiting && TIMER > execution_timeout;
    ok WHEN script IS ok ;
    waiting WHEN script IS working;
    idle DEFAULT;

    COMMAND run { SEND run TO script; WAITFOR script IS working }
    COMMAND abort { DISABLE script; ENABLE script; }
    ENTER error { LOG "error"; CALL abort ON SELF }
}
test_prop_driver Test_PropDriver property_tests;

